---
permalink: /lambda/1
layout: default
---

# Eliminating data types

The most amazing experience I had learning Haskell was discovering _how little_ there is to it. Functions and data types, that is pretty much all there is to it. But there could be even less. This post will show how to replace data types with functions.

### Booleans

Booleans are the simplest nontrivial data type (the simplest begin `()`). It is easy to define them using `data`:

```
data Bool
  = True
  | False
```

To replace them with a function, we must understand how they are used. The universal use case of data types is pattern matching. We could, however, replace pattern matching with the given function:

```
elimBool :: Bool -> r -> r -> r
elimBool True  tt ff = tt
elimBool False tt ff = ff
```

This function _eliminates_ boolean values into the resulting type `r`. With it we can write a very funny looking `not` function:

```
not :: Bool -> Bool
not b = elimBool b False True
```

or `(&&)`:

```
(&&) :: Bool -> Bool -> Bool
b1 && b2 = elimBool b1 b2 False
```

By removing pattern matching, we removed the defining characteristic of data types without losing any functionality. I conjecture that data types _are_ their eliminators. Let's put it to the test.

    {-# LANGUAGE RankNTypes #-}

    module Encode where

    import Prelude hiding ((&&), Bool, Maybe, pred, showList, succ, (||))

    type Bool = forall r. r -> r -> r

    true :: Bool
    true = \tt ff -> tt

    false :: Bool
    false = \tt ff -> ff

    showBool :: Bool -> String
    showBool b = b "True" "False"

Now, let's define a few common functions:

    not :: Bool -> Bool
    not b = \tt ff -> b ff tt

    (&&) :: Bool -> Bool -> Bool
    b1 && b2 = \tt ff -> b1 (b2 tt ff) ff

    (||) :: Bool -> Bool -> Bool
    b1 || b2 = \tt ff -> b1 tt (b2 tt ff)

    infixr 3 &&
    infixr 2 ||

```
λ> showBool (true && (false || true))
"True"
```

Challenge: consider how the `()` eliminator looks like then encode `()` using functions.

### Maybe

Functions can also be used to represent polymorphic types. Let's consider how `Maybe` is pattern matched.

```
elimMaybe :: Maybe a -> (a -> r) -> r -> r
elimMaybe (Just x) f y = f x
elimMaybe Nothing  f y = y
```

A `Maybe a` can be `Just x` or `Nothing`. If it is `Just x`, we may want to do something with `x` before producing the result, so we eliminate it using `f :: a -> r`. If it is nothing, we have no choice but to compute the result.

```
isJust :: Maybe a -> Bool
isJust x = elimMaybe x (const true) false
```

```
λ> showBool (isJust (Just true))
"True"
```

As with `Bool`, `elimMaybe` gives away how to implement `Maybe a` using only functions.

    type Maybe a = forall r. (a -> r) -> r -> r

    just :: a -> Maybe a
    just x = \f y -> f x

    nothing :: Maybe a
    nothing = \f y -> y

    showMaybe :: Show a => Maybe a -> String
    showMaybe x = x (\y -> "Just " ++ show y) "Nothing"

Note that `a` are universally quantified in different scopes. `Maybe a` is a type for all `a`, but `Maybe a` is the type of a "`Maybe`-shaped" function that can be eliminated to return a result `r` for all `r`.

<!-- `Maybe a` knows which `a` it might possibly contain, but it doesn't know which `r` it will be eliminated onto. -->

Our `Maybe` is as general as the "normal" `Maybe`. In fact, it is a monad:

```
fmap :: (a -> b) -> Maybe a -> Maybe b
fmap g x = \f y -> x (f . g) y

return :: a -> Maybe a
return x = \f y -> f x

join :: Maybe (Maybe a) -> Maybe a
join x = \f y -> x (\x -> x f y) y
```

(Unfortunately, Haskell doesn't support polymorphic type synonyms instances.)

Challenge: encode `Either a b` using functions. 
### Natural numbers

Natural numbers can be defined as such:

```
data Nat
  = Zero
  | Succ Nat

zero, one, two :: Nat
zero = Zero
one  = Succ zero
two  = Succ one
```

This is the first recursive type we tackle. Let's see how its eliminator looks like:

```
elimNat :: Nat -> (r -> r) -> r -> r
elimNat Zero     s z = z
elimNat (Succ n) s z = s (elimNat n s z)
```

It is not immediately obvious that this eliminator is the right one. Some examples should make things clearer.

```
instance Show Nat where
  show n = show $ elimNat n succ 0

add :: Nat -> Nat -> Nat
add n m = elimNat n Succ m
```

```
λ> add two two
4
```

A natural number is like an iterator: it applies a function a given number of times.

    type Nat = forall r. (r -> r) -> r -> r

    zero, one, two :: Nat
    zero = \s z -> z
    one  = \s z -> s z
    two  = \s z -> s (s z)

    showNat :: Nat -> Int
    showNat n = n (+1) 0

Of course, we can't construct all natural numbers by hand.

    succ :: Nat -> Nat
    succ n = \s z -> s (n s z)

Challenge: define a predecessor function. The predecessor of zero can be zero or `undefined`.

<!--
    pred :: Nat -> Nat
    pred x = \s z -> snd $ x (\(x, y) -> (s x, x)) (z, undefined)
-->

It is also possible to define arithmetic for `Nat`.

    add :: Nat -> Nat -> Nat
    add n m = \s z -> n s (m s z)

    mul :: Nat -> Nat -> Nat
    mul n m = \s z -> n (m s) z

```
λ> showNat $ add two one
3
λ> showNat $ mul two two
6
```

Challenge: define exponentiation for `Nat`. $$0^0$$ should be one.

### Lists

Last but not least, it is time to put everything together and define lists. Lists are polymorphic, like `Maybe`, and recursive, like `Nat`. As always, let's look at it's eliminator.

```
elimList :: [a] -> (a -> r -> r) -> r -> r
```

This type signature should look eerily familiar. In fact, it is `foldr`!

```
elimList l f z = foldr f z l

append :: [a] -> [a] -> [a]
append l1 l2 = elimList l1 (:) l2
```

    type List a = forall r. (a -> r -> r) -> r -> r

    nil :: List a
    nil = \f z -> z

    cons :: a -> List a -> List a
    cons x l = \f z -> f x (l f z)

    showList :: Show a => List a -> String
    showList l = l (\x s -> "cons (" ++ show x ++ ") (" ++ s ++ ")") "nil"

I can't even begin to tell you how amazing "function encoded" lists are. Contrary to normal lists, we can turn ours into a proper monad transformer. But this is a story for another time. For the extra curious, check [`ListT` done right][1] and [`LogicT`][2].

---

In this post we've seen how to eliminate data types, leaving Haskell only with its most characteristic feature: functions. We've show that you could do everything using named functions. Named functions alone are Turing complete, there is no need for primitive data types. Next time we will go one step further and see how _anonymous_ alone are also Turing complete by introducing untyped lambda calculus.

<!--

Turing machines versus lambda calculus
- Lambda calculus predates Turing machines
- Alonzo Church was Alan Turing's PhD adviser

-->

[1]: https://wiki.haskell.org/ListT_done_right
[2]: http://okmij.org/ftp/papers/LogicT.pdf
